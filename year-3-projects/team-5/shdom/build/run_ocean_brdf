#!/bin/csh
#   Script that runs SHDOM in independent pixel mode for a 2D medium 
# for testing the ocean BRDF surface reflection.  The surface properties
# may change in the X direction.  The atmosphere is a uniform slab with
# Rayleigh scattering and specified optical depth illuminated by the sun.
# Outputs hemispheric flux and radiance at a number of angles.
# Does processing with awk.
set AWK=awk   # use nawk on SGI and Sun


#  Select sections of script to run
set MakePrp=1
set MakeSfc=1
set RunSHDOM=1
set MakePlotFiles=1


#  Choose the file names (property file, surface file, shdom output)
set prpfile="atmos1.prp"
set sfcfile="oceanbrdf1.sfc"
set outbase="oceanbrdf1"

#  Two kinds of plot files:  
#    1) radiance vs angle in the solar plane for one column (fixed BRDF)
#    2) flux vs column (surface parameter)
set plot1file=ocean1ws5.0.grf;   set out1x=9.0
set plot2file=ocean1flux.grf;  set muout=1.0;  set phiout=0


#  Set size of domain (number of IP columns); the column spacing is dx=1.0.
#    The top of the domain is Z=1.0.
set Nx=10

#  Set the solar direction and flux and wavelength
set mu0=-0.7;  set phi0=0.0;  set flux0=1.0
set wavelen=0.50

#  Set the Ocean BRDF parameters:  
#    varying wind speed (m/s) and fixed pigment concentration (mg/m^3)
set windmin=0.5;  set windmax=5.0
set pigment=0.0

#  Set the atmospheric properties
set tau=0.2;  set omega=1.0;  
set Tsfc=300;  set Tbot=300;  set Ttop=250

#  Set some SHDOM parameters: 
set Nmu=16; set Nphi=32       # angular resolution
set Nz=6
set BCflag=0;  set IPflag=3;   set deltaM="T"
set solacc=1.0E-5;  set accel=T;   set maxiter=50
set splitacc=0.001; set shacc=0.0



# ----------------------------------  Start of Processing
# Compile the "put" command
if (!(-e put))  cc -o put  put.c


if ($MakePrp) then
#   Make the shdom atmosphere property file (extinction only format).  
  $AWK -v nx=$Nx -v tb=$Tbot -v tt=$Ttop 'BEGIN {printf "E\n%3.0f %1.0f %1.0f\n%5.3f %3.1f %3.1f %3.1f\n%5.1f %5.1f\n", nx,1,2, 1,1,0,1, tb,tt;}' >! $prpfile
  $AWK -v omega=$omega  'BEGIN { printf "%7.5f  %3.0f %3.1f %3.1f\n", omega, 2, 0, 0.5;}' >> $prpfile
  $AWK -v nx=$Nx -v tau=$tau 'BEGIN {for (i=0; i<nx; i++) {printf "%3.0f %1.0f %6.3f\n", i+1, 1, tau; printf "%3.0f %1.0f %6.3f\n", i+1, 2, tau;} }' >> $prpfile
endif


if ($MakeSfc) then
#  Make the shdom surface file: uniform in Y, increasing parameter in X
#    In this case, have pigment fixed and vary wind speed.
  $AWK -v nx=$Nx -v sfc=O 'BEGIN {printf "%c\n%3.0f %3.0f  %5.2f %5.2f\n", sfc, nx,1, 1,1;}' >! $sfcfile
  $AWK -v nx=$Nx -v wmin=$windmin -v wmax=$windmax -v pig=$pigment  -v temp=$Tsfc \
    'BEGIN {for (ix=1; ix<=nx; ix++) {wspd=wmin+(wmax-wmin)*(ix-1)/(nx-1); printf "%3.0f %3.0f %5.1f %5.2f %5.1f\n", ix,1,temp,wspd,pig;} }' >> $sfcfile
endif



if ($RunSHDOM) then
#   Output the radiance at the top of atmosphere in the solar plane
  set Routparm="1.0  1 1  0 0  19  0.1 180 0.2 180 0.3 180 0.4 180 0.5 180 0.6 180 0.7 180 0.8 180 0.9 180 1.0 0 0.9 0 0.8 0 0.7 0 0.6 0 0.5 0 0.4 0 0.3 0 0.2 0 0.1 0"
#   Output the fluxes at the surface
  set Foutparm="2 0.0  1 1"
#   Run SHDOM
  put OceanBRDF $prpfile $sfcfile NONE NONE NONE "$Nx 1 $Nz" "$Nmu $Nphi" \
     $BCflag $IPflag $deltaM E S "$flux0 $mu0 $phi0" 0.0 $wavelen \
     "$splitacc $shacc" "$accel $solacc $maxiter" \
     2  F $Foutparm ${outbase}f.out  R $Routparm ${outbase}r.out \
     NONE 5 1.2 1.0 1.5 | shdom90
endif



if ($MakePlotFiles) then
#   Makes radiance vs angle in the solar plane for one column
  $AWK -v x=$out1x '{if (index($0,"(mu,phi)")) \
    {mu=$2; phi=$3; th=atan2(sqrt(1-mu^2),mu); \
     if (phi==180) th=-th; th=180*th/3.14159;} \
     if ($1==x) printf "%6.2f %7.4f\n",th,$3;}' ${outbase}r.out >! $plot1file

#   Make radiance vs column (surface parameter) 
#  $AWK -v muo=$muout -v phio=$phiout -v nx=$Nx -v wmin=$windmin -v wmax=$windmax \
#    '{if (index($0,"(mu,phi)")) {if ((muo==$2)&&(phio==$3)) outflag=1; \
#     else outflag=0;} else {if (outflag) {w=wmin+(wmax-wmin)*$1/(nx-1); \
#     printf "%5.2f %7.4f\n",w,$3;}} }' ${outbase}r.out >! $plot2file

#   Make flux vs column (surface parameter) 
  $AWK -v nx=$Nx -v wmin=$windmin -v wmax=$windmax \
    '{if ($1 != "!") {w=wmin+(wmax-wmin)*$1/(nx-1); \
        printf "%5.2f %6.3f\n",w,$3;} }' ${outbase}f.out >! $plot2file
endif


